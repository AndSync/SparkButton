plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.github.connyduck'
version = '3.0.0'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    libraryVariants.all {
        it.generateBuildConfigProvider.configure {
            it.enabled = false
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'sparkbutton'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/sparkbutton-release.aar")
            pom {
                name = 'SparkButton'
                description = 'Android library to create buttons with Twitter\'s heart like animation.'
                url = 'https://github.com/connyduck/sparkbutton'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

tasks.withType(PublishToMavenLocal) {
    dependsOn build
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
}
